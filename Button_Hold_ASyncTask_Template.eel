/* Template and Script developed by Night */

/*

How it works:
1.  Create a sectioncode which should be completely unique.
	If not then it's probably not too much trouble unless another script uses the same Section Code AND a key with name "time".
	Otherwise just change every ExtState modification to what you want.
2.  Set a callstack rate, this is the delay(ms) after a key press until the script ends.
	By default this is set to 500 which appears to be the time taken for fast key input to sink in after initial press.
3.  Set a callstack rate for after the initial time.
	When holding down a key there will be ~500ms where after the first press, none will be detected.
	After this period, the key will send fast key inputs.
	This is where the second callstack rate comes in.
3.  Change the init_press() function for what to happen on the initial script run.
4.  Change what to run in the background with background_tasks().
5.  Change what to run after the key is released inside end_tasks().

The script's initial start time is stored within script_time, string format in str_script_time.

Feel free to message me with feedback, bugs and anything you do with it on Discord! @Night#5537

*/

sectioncode = "12345";
callstack_rate = 500;
callstack_rate_after_init = 40;




function init_press() (
	ShowConsoleMsg("Start\n");
);

function background_tasks() (
	ShowConsoleMsg("Background\n");
);

function end_tasks() (
	ShowConsoleMsg("End\n");
);







// Thankyou to Banned (https://forums.cockos.com/member.php?u=8512) on forums for getting float from string function https://forums.cockos.com/showthread.php?t=140211
function get_float_from_string(str)
(
    matchi("*?-*?%{float_value}f*?", str) ? ( // first, try to match for a negative value, i.e. a (series of) numeric character(s), *directly* preceded with '-' character)
        -float_value; // apply negative sign, return float value
    ) : ( // else, just try to match a (positive) value, i.e. a (series of) numeric character(s)
        matchi("*?%{float_value}f*?", str) ? 
        (
            float_value; // return float value
        );
    );
);

function get_int_from_string(str)
(
    matchi("*?-*?%{float_value}d*?", str) ? ( // first, try to match for a negative value, i.e. a (series of) numeric character(s), *directly* preceded with '-' character)
        -float_value; // apply negative sign, return float value
    ) : ( // else, just try to match a (positive) value, i.e. a (series of) numeric character(s)
        matchi("*?%{float_value}d*?", str) ? 
        (
            float_value; // return float value
        );
    );
);

function getStringFromFloat(float) (
	sprintf(#, "%f", float);
);

function getStringFromInt(int) (
	sprintf(#, "%d", int);
);


function checkCallstack(ms) local(saved_time_after, floatSavedTimeAfter, ms, savedCallstackRate) (
	ms /= 1000;
	time_precise() - script_time < ms ? (
		GetExtState(saved_time_after, sectioncode, "time");
		floatSavedTimeAfter = get_float_from_string(saved_time_after);
		script_time == floatSavedTimeAfter ? (
			HasExtState(sectioncode, "init") ? (
				background_tasks();
			);
			GetExtState(savedCallstackRate, sectioncode, "callstackRate");
			floatSavedCallstackRate = get_int_from_string(savedCallstackRate);
			defer("checkCallstack(floatSavedCallstackRate)");
		):(
			// This will be activated probably at the 2nd key fire
			HasExtState(sectioncode, "init") ? (
				callstack_rate = callstack_rate_after_init;
				SetExtState(sectioncode, "callstackRate", getStringFromInt(callstack_rate), 0);
				DeleteExtState(sectioncode, "init", 1);
			);
			background_tasks();
		);
	):(
		GetExtState(saved_time_after, sectioncode, "time");
		floatSavedTimeAfter = get_float_from_string(saved_time_after);
		script_time == floatSavedTimeAfter ? (
			DeleteExtState(sectioncode, "time", 1);
			DeleteExtState(sectioncode, "init", 1);
			DeleteExtState(sectioncode, "callstackRate", 1);
			end_tasks();
		):(
			HasExtState(sectioncode, "init") ? (
				callstack_rate = callstack_rate_after_init;
				SetExtState(sectioncode, "callstackRate", getStringFromInt(callstack_rate), 0);
				DeleteExtState(sectioncode, "init", 1);
			);
			background_tasks();
		);
	);
);

time_precise(script_time);

sprintf(str_script_time, "%f", script_time);


// Check ExtStates

HasExtState(sectioncode, "time") ? (
	SetExtState(sectioncode, "time", str_script_time, 0);
	GetExtState(savedCallstackRate, sectioncode, "callstackRate");
	floatSavedCallstackRate = get_int_from_string(savedCallstackRate);
	defer("checkCallstack(floatSavedCallstackRate)");
):(
	SetExtState(sectioncode, "time", str_script_time, 0);
	SetExtState(sectioncode, "init", "pressed", 0);
	SetExtState(sectioncode, "callstackRate", getStringFromInt(callstack_rate), 0);
	init_press();
	defer("checkCallstack(callstack_rate)");
);